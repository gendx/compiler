/*
    Compiler - a test of flexc++/bisonc++ for a small programming language.
    Copyright (C) 2014 - 2015  G. Endignoux

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see http://www.gnu.org/licenses/gpl-3.0.txt
*/

%x COMMENT
%x CHAR
%x STRING
%x NEWLINE

%%


// ******************** Keywords ********************
// Classes
class {
    *d_val = makeToken();
    return Parser::CLASS; }
concept {
    *d_val = makeToken();
    return Parser::CONCEPT; }
def {
    *d_val = makeToken();
    return Parser::DEF; }

// Flow control
return {
    *d_val = makeToken();
    return Parser::RETURN; }
break {
    *d_val = makeToken();
    return Parser::BREAK; }
continue {
    *d_val = makeToken();
    return Parser::CONTINUE; }
pass {
    *d_val = makeToken();
    return Parser::PASS; }

// Loops
forever {
    *d_val = makeToken();
    return Parser::FOREVER; }
while {
    *d_val = makeToken();
    return Parser::WHILE; }
for {
    *d_val = makeToken();
    return Parser::FOR; }
in {
    *d_val = makeToken();
    return Parser::IN; }

// Conditions
if {
    *d_val = makeToken();
    return Parser::IF; }
elif {
    *d_val = makeToken();
    return Parser::ELIF; }
else {
    *d_val = makeToken();
    return Parser::ELSE; }
switch {
    *d_val = makeToken();
    return Parser::SWITCH; }
case {
    *d_val = makeToken();
    return Parser::CASE; }

// Filtering
match {
    *d_val = makeToken();
    return Parser::MATCH; }
with {
    *d_val = makeToken();
    return Parser::WITH; }
as {
    *d_val = makeToken();
    return Parser::AS; }
default {
    *d_val = makeToken();
    return Parser::DEFAULT; }


// ******************** Basic data ********************
[A-Za-z_][A-Za-z0-9_]* {
    *d_val = makeToken();
    return Parser::IDENTIFIER; }
0x[0-9A-Fa-f]+                      |
0b[01]+                             |
[+-]?[0-9]*\.[0-9]+e[+-]?[0-9]+     |
[+-]?[0-9]+(\.[0-9]*)?e[+-]?[0-9]+  |
[+-]?[0-9]*\.[0-9]+                 |
[+-]?[0-9]+(\.[0-9]*)? {
    *d_val = makeToken();
    return Parser::NUMBER; }


// ******************** Operators ********************
"(" {
    *d_val = makeToken();
    return Parser::LEFT_PAR; }
")" {
    *d_val = makeToken();
    return Parser::RIGHT_PAR; }
"[" {
    *d_val = makeToken();
    return Parser::LEFT_BRACKET; }
"]" {
    *d_val = makeToken();
    return Parser::RIGHT_BRACKET; }
"," {
    *d_val = makeToken();
    return Parser::COMMA; }
".." {
    *d_val = makeToken();
    return Parser::RANGE; }
"." {
    *d_val = makeToken();
    return Parser::DOT; }

"**="|"+="|"-="|"*="|"/="|"%="|"<<="|">>="|"^="|"|="|"&="|":=" {
    *d_val = makeToken();
    return Parser::OP_ASSIGN; }
"||" {
    *d_val = makeToken();
    return Parser::COND_OR; }
"&&" {
    *d_val = makeToken();
    return Parser::COND_AND; }

"**" {
    *d_val = makeToken();
    return Parser::POW; }
"++"|"--" {
    *d_val = makeToken();
    return Parser::OP_UNARY; }
"*"|"/"|"%" {
    *d_val = makeToken();
    return Parser::OP_FACTOR; }
"+"|"-" {
    *d_val = makeToken();
    return Parser::OP_TERM; }
"<<"|">>" {
    *d_val = makeToken();
    return Parser::OP_SHIFT; }
"&" {
    *d_val = makeToken();
    return Parser::AND; }
"^" {
    *d_val = makeToken();
    return Parser::XOR; }
"|" {
    *d_val = makeToken();
    return Parser::OR; }

"<="|">="|"!="|"<"|">"|"==" {
    *d_val = makeToken();
    return Parser::OP_COMPARE; }
"~"|"!" {
    *d_val = makeToken();
    return Parser::OP_UNARY; }

":" {
    *d_val = makeToken();
    return Parser::COLON; }


// ******************** Comment ********************
\/\/.*$ {
    // Ignore single-line comment.
}

\/\* {
    more();
    begin(StartCondition__::COMMENT);
}
<COMMENT> {
    \*\/ {
        // Ignore multi-line comment.
        begin(StartCondition__::INITIAL);
    }
    
    .|\n {
        more();
    }
}


// ******************** Char ********************
\' {
    more();
    begin(StartCondition__::CHAR);
}
<CHAR> {
    \' {
        begin(StartCondition__::INITIAL);
        *d_val = makeToken();
        return Parser::CHAR;
    }
    
    \\.|. {
        more();
    }
    
    \n {
        *d_val = makeToken();
        return Parser::ERR_CHAR;
    }
}


// ******************** String ********************
\" {
    more();
    begin(StartCondition__::STRING);
}
<STRING> {
    \" {
        begin(StartCondition__::INITIAL);
        *d_val = makeToken();
        return Parser::STRING;
    }
    
    \\.|. {
        more();
    }
    
    \n {
        *d_val = makeToken();
        return Parser::ERR_STRING;
    }
}


// ******************** Indentation ********************
<NEWLINE>^[ \t]*/\n {
    // Ignore empty line.
    begin(StartCondition__::INITIAL);
}

<NEWLINE>^[ \t]*\/\/ {
    // Ignore empty line before comment.
    begin(StartCondition__::INITIAL);
    push('/');
    push('/');
}

<NEWLINE>^[ \t]*\/\* {
    // Ignore empty line before comment.
    begin(StartCondition__::INITIAL);
    push('*');
    push('/');
}

<NEWLINE>^[ \t]*/[^ \t\n] {
    begin(StartCondition__::INITIAL);

    unsigned int indent = 0;
    for (unsigned int i = 0 ; i < length() ; ++i)
        if (matched()[i] == ' ')
            ++indent;
        else
            indent = (indent + 4) & (~3);

    if (!mIsDedent)
        mIndentToken = makeIndentToken(indent);
    mIsDedent = false;
    *d_val = mToken = mIndentToken;

    if (mIndentLevels.empty())
    {
        // No previous indent.
        if (indent > 0)
        {
            mIndentLevels.push(indent);
            return Parser::INDENT;
        }
    }
    else if (indent > mIndentLevels.top())
    {
        // Indent.
        mIndentLevels.push(indent);
        return Parser::INDENT;
    }
    else if (indent < mIndentLevels.top())
    {
        // Dedent.
        mIndentLevels.pop();
        unsigned int last = mIndentLevels.empty() ? 0 : mIndentLevels.top();

        if (indent > last)
        {
            std::cout << "Indentation error." << std::endl;
            return Parser::ERR_INDENT;
        }
        if (indent <= last)
        {
            for (unsigned int i = 0 ; i < indent ; ++i)
                push(' ');
        }
        push('\n');

        mIsDedent = true;
        return Parser::DEDENT;
    }
}

<NEWLINE><<EOF>> {
    begin(StartCondition__::INITIAL);

    if (!mIsDedent)
        mIndentToken = makeIndentToken(0);
    mIsDedent = false;
    *d_val = mToken = mIndentToken;

    if (!mIndentLevels.empty())
    {
        mIndentLevels.pop();

        if (!mIndentLevels.empty())
        {
            unsigned int indent = mIndentLevels.top();
            for (unsigned int i = 0 ; i < indent ; ++i)
                push(' ');
        }
        push('\n');

        mIsDedent = true;
        return Parser::DEDENT;
    }
    else
    {
        // Return EOF.
        leave(0);
    }
}


// ******************** EOF ********************
<<EOF>> {
    push('\n');
}


// ******************** Spaces ********************
[ \t]+ {
    // Ignore spaces.
}
\n {
    begin(StartCondition__::NEWLINE);
    *d_val = mToken = makeEOLToken();
    return Parser::EOL; }


// Unknown
. {
    *d_val = makeToken();
    return Parser::UNKNOWN_CHAR; }

